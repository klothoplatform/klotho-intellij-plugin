{
  parserClass="com.github.cloudcompilers.klotho.language.psi.KlothoParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Klotho"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.cloudcompilers.klotho.language.psi"
  psiImplPackage="com.github.cloudcompilers.klotho.language.psi.impl"

  elementTypeHolderClass="com.github.cloudcompilers.klotho.language.psi.KlothoTypes"
  elementTypeClass="com.github.cloudcompilers.klotho.language.parsing.KlothoElementType"
  tokenTypeClass="com.github.cloudcompilers.klotho.language.parsing.KlothoTokenType"

//  psiImplUtilClass="com.github.cloudcompilers.klotho.language.KlothoPsiImplUtil"

  tokens=[
    PLAINTEXT
    EOL='[\r\n]'
    LEFT_BRACE='{'
    RIGHT_BRACE='}'
    space='regexp:\s+'
    EQ='='
    LEFT_BRACKET='['
    RIGHT_BRACKET=']'
    STAR='*'
    COMMA=','
    BOOLEAN='regexp:(true|false)'

    OFFSET_DATE_TIME
    LOCAL_DATE_TIME
    LOCAL_DATE
    LOCAL_TIME

    TRIPLE_QUOTE
    MULTILINE_STRING_CONTENT
    MULTILINE_LINE_SEPARATOR

    string="regexp:('(([^'][^']|[^'\\])|\\.)*'|\"(([^\"][^\"]|[^\"\\])|\\.)*\")"

    PLAIN_NUMBER = 'regexp:\d'
    HEX_PREFIX = '0x'
    OCT_PREFIX = '0o'
    BIN_PREFIX = '0b'

    DIG0_1 = 'regexp:[0-1]+'
    DIG0_7 = 'regexp:[0-7]+'
    HEX_DIG = 'regexp:[A-Fa-f]+'

    TOML_COMMENT='regexp:#.*'

    ANNOTATION='@klotho'
    SEPARATOR='::'

    ID='regexp:\p{Alpha}[\w-.]*'
    HEADER_ID='regexp:\p{Alpha}[\w-.]*'
    CAPABILITY='regexp:\p{Alpha}\w*'

    JSDOC_COMMENT_START='regexp:/\*\*\s?'
    MULTILINE_COMMENT_START='/*'
    MULTILINE_COMMENT_END='*/'
    C_LINE_COMMENT  = '//'
    PY_COMMENT = '#'
  ]
}

// see Grammar Kit: https://github.com/JetBrains/Grammar-Kit

Root ::= (JSDocCommentBlock | CStyleCommentBlock | LineComment | RawComment) + <<eof>>

JSDocCommentBlock ::= JSDOC_COMMENT_START EOL * RawComment *  (StarPrefix AnnotationExpr RawComment *) + MULTILINE_COMMENT_END

CStyleCommentBlock ::= MULTILINE_COMMENT_START EOL * RawComment * (StarPrefix AnnotationExpr RawComment *) + MULTILINE_COMMENT_END

LineComment ::= LineCommentPrefix AnnotationExpr

AnnotationExpr ::= '@klotho' '::' CAPABILITY [ AnnotationBody ] {pin=3}

AnnotationBody ::= StarPrefix '{' EOL * AnnotationContent  EOL * StarPrefix '}' EOL * {pin=2}

private AnnotationContent ::=  [Statement *]

Statement ::= StarPrefix ((AnyExpr [TOML_COMMENT]) | TOML_COMMENT)  [EOL]

private AnyExpr ::=  [AssignmentExpr | SectionHeader] EOL

AssignmentExpr ::= Key '=' Value {pin=2}

private StarPrefix ::=  ('*' + | LineCommentPrefix) *

private LineCommentPrefix ::= '#' + | '//' +

SectionHeader ::= '[' HEADER_ID ']' {pin=1}

private Value ::= SimpleValue | Array | InlineTable

InlineTable ::= '{' [AssignmentExpr] (',' AssignmentExpr) * '}' {pin(".*")=1}

Array ::= '[' ([(Value ( [StarPrefix] ',' [StarPrefix] )) * [StarPrefix] [Value] !','] )  ']' {pin=2}

private SimpleValue ::=  Number | string  | MultilineString | BOOLEAN | DateTimeLiteral

Key ::= ID

Number ::= PLAIN_NUMBER | HexNumber | BinNumber | OctNumber

OctNumber ::= OCT_PREFIX DIG0_7 {pin=1}

BinNumber ::= BIN_PREFIX DIG0_1 {pin=1}

HexNumber ::= HEX_PREFIX HEX_DIG {pin=1}

RawComment ::= StarPrefix PLAINTEXT * EOL

MultilineString ::= TRIPLE_QUOTE (MULTILINE_LINE_SEPARATOR | MULTILINE_STRING_CONTENT) * TRIPLE_QUOTE

private DateTimeLiteral ::= OFFSET_DATE_TIME | LOCAL_DATE_TIME | LOCAL_DATE | LOCAL_TIME
